# -- gdbundle_BEGIN
python
import os,sys,subprocess
# Execute a Python using the user's shell and pull out the sys.path (for site-packages)
paths = subprocess.check_output('python -c "import os,sys;print(os.linesep.join(sys.path).strip())"',shell=True).decode("utf-8").split()
# Extend GDB's Python search path
sys.path.extend(paths)

# Initialize gdbundle and bundles
import gdbundle
# Load any gdbundle-* packages by default
gdbundle.init()

end
# -- gdbundle_END

# connect to openocd
target extended-remote :3333

define openocd-reload
    # reset, program, reset via openocd
    monitor reset halt
    load
    monitor reset init

    # rtt server setup
    monitor rtt stop
    # use eval to generate an integer argument to 'rtt setup' for the RTT
    # control block address
    eval "monitor rtt setup %d 2048 \"SEGGER RTT\"", &_SEGGER_RTT
    monitor rtt server stop 9090
    monitor rtt server start 9090 0
    # initialize the monitor structure now, so we can start rtt without waiting
    # for the application to initialize it
    call SEGGER_RTT_Init()
    monitor rtt start
end
document openocd-reload
Reload elf and reflash
end

define jlink-reload
    # program and reset via jlink
    monitor reset
    monitor semihosting enable
    load build/main.elf
    monitor reset
    monitor halt
end
document jlink-reload
Reload elf and reflash
end

define pyocd-reload
    # program + reset via pyocd
    monitor reset halt
    load build/main.elf
    monitor reset halt
end
document pyocd-reload
Reload elf and reflash
end
